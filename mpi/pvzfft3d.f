C
C     FFTE: A FAST FOURIER TRANSFORM PACKAGE
C
C     (C) COPYRIGHT SOFTWARE, 2000-2004, 2008-2011, ALL RIGHTS RESERVED
C                BY
C         DAISUKE TAKAHASHI
C         FACULTY OF ENGINEERING, INFORMATION AND SYSTEMS
C         UNIVERSITY OF TSUKUBA
C         1-1-1 TENNODAI, TSUKUBA, IBARAKI 305-8573, JAPAN
C         E-MAIL: daisuke@cs.tsukuba.ac.jp
C
C
C     PARALLEL 3-D COMPLEX FFT ROUTINE (FOR VECTOR MACHINES)
C
C     FORTRAN77 + MPI SOURCE PROGRAM
C
C     CALL PZFFT3D(A,B,NX,NY,NZ,ICOMM,NPU,IOPT)
C
C     NX IS THE LENGTH OF THE TRANSFORMS IN THE X-DIRECTION (INTEGER*4)
C     NY IS THE LENGTH OF THE TRANSFORMS IN THE Y-DIRECTION (INTEGER*4)
C     NZ IS THE LENGTH OF THE TRANSFORMS IN THE Z-DIRECTION (INTEGER*4)
C       ------------------------------------
C         NX = (2**IP) * (3**IQ) * (5**IR)
C         NY = (2**JP) * (3**JQ) * (5**JR)
C         NZ = (2**KP) * (3**KQ) * (5**KR)
C       ------------------------------------
C     ICOMM IS THE COMMUNICATOR (INTEGER*4)
C     NPU IS THE NUMBER OF PROCESSORS (INTEGER*4)
C     IOPT = 0 FOR INITIALIZING THE COEFFICIENTS (INTEGER*4)
C     IOPT = -1 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY,NZ/NPU) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,*,BLOCK)
C              B(NX,NY,NZ/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,*,BLOCK)
C     IOPT = +1 FOR INVERSE TRANSFORM WHERE
C              A(NX,NY,NZ/NPU) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,*,BLOCK)
C              B(NX,NY,NZ/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,*,BLOCK)
C     IOPT = -2 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY,NZ/NPU) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,*,BLOCK)
C              B(NX/NPU,NY,NZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(BLOCK,*,*)
C     IOPT = +2 FOR INVERSE TRANSFORM WHERE
C              A(NX/NPU,NY,NZ) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(BLOCK,*,*)
C              B(NX,NY,NZ/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,*,BLOCK)
C
C     WRITTEN BY DAISUKE TAKAHASHI
C
      SUBROUTINE PZFFT3D(A,B,NX,NY,NZ,ICOMM,NPU,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'param.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 WX(NDA3),WY(NDA3),WZ(NDA3)
      DIMENSION LNX(3),LNY(3),LNZ(3)
      SAVE WX,WY,WZ
C
      NN=NX*NY*(NZ/NPU)
C
      CALL FACTOR(NX,LNX)
      CALL FACTOR(NY,LNY)
      CALL FACTOR(NZ,LNZ)
C
      IF (IOPT .EQ. 0) THEN
        CALL SETTBL(WX,NX)
        CALL SETTBL(WY,NY)
        CALL SETTBL(WZ,NZ)
        RETURN
      END IF
C
      IF (IOPT .EQ. 1 .OR. IOPT .EQ. 2) THEN
!DIR$ VECTOR ALIGNED
        DO 10 I=1,NN
          A(I)=DCONJG(A(I))
   10   CONTINUE
      END IF
C
      IF (IOPT .EQ. -1 .OR. IOPT .EQ. -2) THEN
        CALL PZFFT3DF(A,B,WX,WY,WZ,NX,NY,NZ,LNX,LNY,LNZ,ICOMM,NPU,IOPT)
      ELSE
        CALL PZFFT3DB(A,B,WX,WY,WZ,NX,NY,NZ,LNX,LNY,LNZ,ICOMM,NPU,IOPT)
      END IF
C
      IF (IOPT .EQ. 1 .OR. IOPT .EQ. 2) THEN
        DN=1.0D0/(DBLE(NX)*DBLE(NY)*DBLE(NZ))
!DIR$ VECTOR ALIGNED
        DO 20 I=1,NN
          B(I)=DCONJG(B(I))*DN
   20   CONTINUE
      END IF
      RETURN
      END
      SUBROUTINE PZFFT3DF(A,B,WX,WY,WZ,NX,NY,NZ,LNX,LNY,LNZ,ICOMM,NPU,
     1                    IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 WX(*),WY(*),WZ(*)
      DIMENSION LNX(*),LNY(*),LNZ(*)
C
      NNX=NX/NPU
      NNZ=NZ/NPU
      NN=NX*NY*NNZ
C
      CALL ZTRANS(A,B,NX,NY*NNZ)
      CALL MFFT235A(B,A,WX,NY*NNZ,NX,LNX)
      CALL ZTRANS(B,A,NY,NNZ*NX)
      CALL MFFT235A(A,B,WY,NNZ*NX,NY,LNY)
      CALL MZTRANSA(A,B,NNZ*NNX,NPU,NY)
      CALL MZTRANSB(B,A,NNZ,NNX*NY,NPU)
      CALL MPI_ALLTOALL(A,NN/NPU,MPI_DOUBLE_COMPLEX,B,NN/NPU,
     1                  MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      CALL MFFT235A(B,A,WZ,NNX*NY,NZ,LNZ)
      IF (IOPT .EQ. -2) RETURN
      CALL MPI_ALLTOALL(B,NN/NPU,MPI_DOUBLE_COMPLEX,A,NN/NPU,
     1                  MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      CALL MZTRANSA(A,B,NNX,NY*NNZ,NPU)
      RETURN
      END
      SUBROUTINE PZFFT3DB(A,B,WX,WY,WZ,NX,NY,NZ,LNX,LNY,LNZ,ICOMM,NPU,
     1                    IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 WX(*),WY(*),WZ(*)
      DIMENSION LNX(*),LNY(*),LNZ(*)
C
      NNX=NX/NPU
      NNZ=NZ/NPU
      NN=NX*NY*NNZ
C
      IF (IOPT .EQ. 1) THEN
        CALL MZTRANSA(A,B,NNX,NPU,NY*NNZ)
        CALL MPI_ALLTOALL(B,NN/NPU,MPI_DOUBLE_COMPLEX,A,NN/NPU,
     1                    MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      END IF
C
      CALL MFFT235A(A,B,WZ,NNX*NY,NZ,LNZ)
      CALL MPI_ALLTOALL(A,NN/NPU,MPI_DOUBLE_COMPLEX,B,NN/NPU,
     1                  MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      CALL MZTRANSB(B,A,NNX*NY,NNZ,NPU)
      CALL MZTRANSA(A,B,NNZ*NNX,NY,NPU)
      CALL MFFT235A(B,A,WY,NNZ*NX,NY,LNY)
      CALL ZTRANS(B,A,NNZ*NX,NY)
      CALL MFFT235A(A,B,WX,NY*NNZ,NX,LNX)
      CALL ZTRANS(A,B,NY*NNZ,NX)
      RETURN
      END
