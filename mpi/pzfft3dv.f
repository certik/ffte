C
C     FFTE: A FAST FOURIER TRANSFORM PACKAGE
C
C     (C) COPYRIGHT SOFTWARE, 2000-2004, 2008-2014, 2020
C         ALL RIGHTS RESERVED
C                BY
C         DAISUKE TAKAHASHI
C         CENTER FOR COMPUTATIONAL SCIENCES
C         UNIVERSITY OF TSUKUBA
C         1-1-1 TENNODAI, TSUKUBA, IBARAKI 305-8577, JAPAN
C         E-MAIL: daisuke@cs.tsukuba.ac.jp
C
C
C     PARALLEL 3-D COMPLEX FFT ROUTINE (WITH 2-D DECOMPOSITION)
C
C     FORTRAN77 + MPI SOURCE PROGRAM
C
C     CALL PZFFT3DV(A,B,NX,NY,NZ,ICOMMY,ICOMMZ,NPUY,NPUZ,IOPT)
C
C     NX IS THE LENGTH OF THE TRANSFORMS IN THE X-DIRECTION (INTEGER*4)
C     NY IS THE LENGTH OF THE TRANSFORMS IN THE Y-DIRECTION (INTEGER*4)
C     NZ IS THE LENGTH OF THE TRANSFORMS IN THE Z-DIRECTION (INTEGER*4)
C       ------------------------------------
C         NX = (2**IP) * (3**IQ) * (5**IR)
C         NY = (2**JP) * (3**JQ) * (5**JR)
C         NZ = (2**KP) * (3**KQ) * (5**KR)
C       ------------------------------------
C     ICOMMY IS THE COMMUNICATOR IN THE Y-DIRECTION (INTEGER*4)
C     ICOMMZ IS THE COMMUNICATOR IN THE Z-DIRECTION (INTEGER*4)
C     NPUY IS THE NUMBER OF PROCESSORS IN THE Y-DIRECTION (INTEGER*4)
C     NPUZ IS THE NUMBER OF PROCESSORS IN THE Z-DIRECTION (INTEGER*4)
C     IOPT = 0 FOR INITIALIZING THE COEFFICIENTS (INTEGER*4)
C     IOPT = -1 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY/NPUY,NZ/NPUZ) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,BLOCK,BLOCK)
C              B(NX,NY/NPUY,NZ/NPUZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,BLOCK,BLOCK)
C     IOPT = +1 FOR INVERSE TRANSFORM WHERE
C              A(NX,NY/NPUY,NZ/NPUZ) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,BLOCK,BLOCK)
C              B(NX,NY/NPUY,NZ/NPUZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,BLOCK,BLOCK)
C     IOPT = -2 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY/NPUY,NZ/NPUZ) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,BLOCK,BLOCK)
C              B(NX/NPUY,NY/NPUZ,NZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(BLOCK,BLOCK,*)
C     IOPT = +2 FOR INVERSE TRANSFORM WHERE
C              A(NX/NPUY,NY/NPUZ,NZ) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(BLOCK,BLOCK,*)
C              B(NX,NY/NPUY,NZ/NPUZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,BLOCK,BLOCK)
C
C     WRITTEN BY DAISUKE TAKAHASHI
C
      SUBROUTINE PZFFT3DV(A,B,NX,NY,NZ,ICOMMY,ICOMMZ,NPUY,NPUZ,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'param.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 C(NDA3)
      COMPLEX*16 WX(NDA3),WY(NDA3),WZ(NDA3)
      DIMENSION LNX(3),LNY(3),LNZ(3)
      SAVE WX,WY,WZ
C
      NN=NX*(NY/NPUY)*(NZ/NPUZ)
C
      IF (IOPT .EQ. 0) THEN
        CALL SETTBL(WX,NX)
        CALL SETTBL(WY,NY)
        CALL SETTBL(WZ,NZ)
        RETURN
      END IF
C
      IF (IOPT .EQ. 1 .OR. IOPT .EQ. 2) THEN
!$OMP PARALLEL DO
!DIR$ VECTOR ALIGNED
        DO 10 I=1,NN
          A(I)=DCONJG(A(I))
   10   CONTINUE
      END IF
C
      CALL FACTOR(NX,LNX)
      CALL FACTOR(NY,LNY)
      CALL FACTOR(NZ,LNZ)
C
      IF (IOPT .EQ. -1 .OR. IOPT .EQ. -2) THEN
!$OMP PARALLEL PRIVATE(C)
        CALL PZFFT3DVF(A,A,A,A,A,A,A,B,B,B,B,B,B,B,C,WX,WY,WZ,NX,NY,NZ,
     1                 LNX,LNY,LNZ,ICOMMY,ICOMMZ,NPUY,NPUZ,IOPT)
!$OMP END PARALLEL
      ELSE
!$OMP PARALLEL PRIVATE(C)
        CALL PZFFT3DVB(A,A,A,A,A,A,A,B,B,B,B,B,B,B,C,WX,WY,WZ,NX,NY,NZ,
     1                 LNX,LNY,LNZ,ICOMMY,ICOMMZ,NPUY,NPUZ,IOPT)
!$OMP END PARALLEL
      END IF
C
      IF (IOPT .EQ. 1 .OR. IOPT .EQ. 2) THEN
        DN=1.0D0/(DBLE(NX)*DBLE(NY)*DBLE(NZ))
!$OMP PARALLEL DO
!DIR$ VECTOR ALIGNED
        DO 20 I=1,NN
          B(I)=DCONJG(B(I))*DN
   20   CONTINUE
      END IF
      RETURN
      END
      SUBROUTINE PZFFT3DVF(A,AXYZP,AXZYP,AYZXP,AZPXY,AZXY,AZXY2,B,BXPYZ,
     1                     BXZYP,BYPZX,BYZX,BYZX2,BZXYP,C,WX,WY,WZ,
     2                     NX,NY,NZ,LNX,LNY,LNZ,ICOMMY,ICOMMZ,NPUY,NPUZ,
     3                     IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      INCLUDE 'param.h'
      COMPLEX*16 A(NX,*),AXYZP(NX/NPUY,NY/NPUZ,NZ/NPUZ,*),
     1           AXZYP(NX/NPUY,NZ/NPUZ,NY/NPUY,*),
     2           AYZXP(NY/NPUY,(NZ/NPUZ)*(NX/NPUY),*),
     3           AZPXY(NZ/NPUZ,NPUZ,*),AZXY(NZ,*),
     4           AZXY2((NZ/NPUZ)*(NX/NPUY),*)
      COMPLEX*16 B((NX/NPUY)*(NY/NPUZ),*),BXPYZ(NX/NPUY,NPUY,NY/NPUY,*),
     1           BXZYP(NX/NPUY,NZ/NPUZ,NY/NPUZ,*),BYPZX(NY/NPUY,NPUY,*),
     2           BYZX(NY,*),BYZX2((NY/NPUY)*(NZ/NPUZ),*),
     3           BZXYP(NZ/NPUZ,(NX/NPUY)*(NY/NPUZ),*)
      COMPLEX*16 C(*)
      COMPLEX*16 WX(*),WY(*),WZ(*)
      DIMENSION LNX(*),LNY(*),LNZ(*)
C
      NNXY=NX/NPUY
      NNYY=NY/NPUY
      NNYZ=NY/NPUZ
      NNZZ=NZ/NPUZ
      NN=NX*NNYY*NNZZ
C
!$OMP DO
      DO 10 J=1,NNYY*NNZZ
        CALL FFT235(A(1,J),C,WX,NX,LNX)
   10 CONTINUE
!$OMP DO COLLAPSE(2) PRIVATE(I,J,JJ)
      DO 50 II=1,NX,NBLK
        DO 40 JJ=1,NNYY*NNZZ,NBLK
          DO 30 I=II,MIN0(II+NBLK-1,NX)
!DIR$ VECTOR ALIGNED
            DO 20 J=JJ,MIN0(JJ+NBLK-1,NNYY*NNZZ)
              BYZX2(J,I)=A(I,J)
   20       CONTINUE
   30     CONTINUE
   40   CONTINUE
   50 CONTINUE
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALL(BYZX2,NN/NPUY,MPI_DOUBLE_COMPLEX,AYZXP,NN/NPUY,
     1                  MPI_DOUBLE_COMPLEX,ICOMMY,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO PRIVATE(J,L)
      DO 80 K=1,NNZZ*NNXY
        DO 70 L=1,NPUY
!DIR$ VECTOR ALIGNED
          DO 60 J=1,NNYY
            BYPZX(J,L,K)=AYZXP(J,K,L)
   60     CONTINUE
   70   CONTINUE
        CALL FFT235(BYZX(1,K),C,WY,NY,LNY)
   80 CONTINUE
!$OMP DO COLLAPSE(2) PRIVATE(J,K,KK)
      DO 120 JJ=1,NY,NBLK
        DO 110 KK=1,NNZZ*NNXY,NBLK
          DO 100 J=JJ,MIN0(JJ+NBLK-1,NY)
!DIR$ VECTOR ALIGNED
            DO 90 K=KK,MIN0(KK+NBLK-1,NNZZ*NNXY)
              AZXY2(K,J)=BYZX(J,K)
   90       CONTINUE
  100     CONTINUE
  110   CONTINUE
  120 CONTINUE
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALL(AZXY2,NN/NPUZ,MPI_DOUBLE_COMPLEX,BZXYP,NN/NPUZ,
     1                  MPI_DOUBLE_COMPLEX,ICOMMZ,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO PRIVATE(K,L)
      DO 150 I=1,NNXY*NNYZ
        DO 140 L=1,NPUZ
!DIR$ VECTOR ALIGNED
          DO 130 K=1,NNZZ
            AZPXY(K,L,I)=BZXYP(K,I,L)
  130     CONTINUE
  140   CONTINUE
        CALL FFT235(AZXY(1,I),C,WZ,NZ,LNZ)
  150 CONTINUE
!$OMP DO COLLAPSE(2) PRIVATE(I,II,K)
      DO 190 KK=1,NZ,NBLK
        DO 180 II=1,NNXY*NNYZ,NBLK
          DO 170 K=KK,MIN0(KK+NBLK-1,NZ)
!DIR$ VECTOR ALIGNED
            DO 160 I=II,MIN0(II+NBLK-1,NNXY*NNYZ)
              B(I,K)=AZXY(K,I)
  160       CONTINUE
  170     CONTINUE
  180   CONTINUE
  190 CONTINUE
      IF (IOPT .EQ. -2) RETURN
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALL(B,NN/NPUZ,MPI_DOUBLE_COMPLEX,AXYZP,NN/NPUZ,
     1                  MPI_DOUBLE_COMPLEX,ICOMMZ,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO COLLAPSE(2) PRIVATE(I,J,K)
      DO 230 L=1,NPUZ
        DO 220 J=1,NNYZ
          DO 210 K=1,NNZZ
!DIR$ VECTOR ALIGNED
            DO 200 I=1,NNXY
              BXZYP(I,K,J,L)=AXYZP(I,J,K,L)
  200       CONTINUE
  210     CONTINUE
  220   CONTINUE
  230 CONTINUE
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALL(BXZYP,NN/NPUY,MPI_DOUBLE_COMPLEX,AXZYP,NN/NPUY,
     1                  MPI_DOUBLE_COMPLEX,ICOMMY,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO COLLAPSE(2) PRIVATE(I,J,L)
      DO 270 K=1,NNZZ
        DO 260 J=1,NNYY
          DO 250 L=1,NPUY
!DIR$ VECTOR ALIGNED
            DO 240 I=1,NNXY
              BXPYZ(I,L,J,K)=AXZYP(I,K,J,L)
  240       CONTINUE
  250     CONTINUE
  260   CONTINUE
  270 CONTINUE
      RETURN
      END
      SUBROUTINE PZFFT3DVB(A,AXPYZ,AXZYP,AYPZX,AYZX,AYZX2,AZXYP,B,BXYZP,
     1                     BXZYP,BYZXP,BZPXY,BZXY,BZXY2,C,WX,WY,WZ,
     2                     NX,NY,NZ,LNX,LNY,LNZ,ICOMMY,ICOMMZ,NPUY,NPUZ,
     3                     IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      INCLUDE 'param.h'
      COMPLEX*16 A((NX/NPUY)*(NY/NPUZ),*),AXPYZ(NX/NPUY,NPUY,NY/NPUY,*),
     1           AXZYP(NX/NPUY,NZ/NPUZ,NY/NPUZ,*),
     2           AYPZX(NY/NPUY,NPUY,*),AYZX(NY,*),
     3           AYZX2((NY/NPUY)*(NZ/NPUZ),*),
     4           AZXYP(NZ/NPUZ,(NX/NPUY)*(NY/NPUZ),*)
      COMPLEX*16 B(NX,*),BXYZP(NX/NPUY,NY/NPUZ,NZ/NPUZ,*),
     1           BXZYP(NX/NPUY,NZ/NPUZ,NY/NPUY,*),
     2           BYZXP(NY/NPUY,(NZ/NPUZ)*(NX/NPUY),*),
     3           BZPXY(NZ/NPUZ,NPUZ,*),BZXY(NZ,*),
     4           BZXY2((NZ/NPUZ)*(NX/NPUY),*)
      COMPLEX*16 C(*)
      COMPLEX*16 WX(*),WY(*),WZ(*)
      DIMENSION LNX(*),LNY(*),LNZ(*)
C
      NNXY=NX/NPUY
      NNYY=NY/NPUY
      NNYZ=NY/NPUZ
      NNZZ=NZ/NPUZ
      NN=NNXY*NNYZ*NZ
C
      IF (IOPT .EQ. 1) THEN
!$OMP DO COLLAPSE(2) PRIVATE(I,J,K)
        DO 40 L=1,NPUY
          DO 30 J=1,NNYY
            DO 20 K=1,NNZZ
!DIR$ VECTOR ALIGNED
              DO 10 I=1,NNXY
                BXZYP(I,K,J,L)=AXPYZ(I,L,J,K)
   10         CONTINUE
   20       CONTINUE
   30     CONTINUE
   40   CONTINUE
!$OMP BARRIER
!$OMP MASTER
        CALL MPI_ALLTOALL(BXZYP,NN/NPUY,MPI_DOUBLE_COMPLEX,AXZYP,
     1                    NN/NPUY,MPI_DOUBLE_COMPLEX,ICOMMY,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO COLLAPSE(2) PRIVATE(I,J,K)
        DO 80 L=1,NPUZ
          DO 70 K=1,NNZZ
            DO 60 J=1,NNYZ
!DIR$ VECTOR ALIGNED
              DO 50 I=1,NNXY
                BXYZP(I,J,K,L)=AXZYP(I,K,J,L)
   50         CONTINUE
   60       CONTINUE
   70     CONTINUE
   80   CONTINUE
!$OMP BARRIER
!$OMP MASTER
        CALL MPI_ALLTOALL(BXYZP,NN/NPUZ,MPI_DOUBLE_COMPLEX,A,NN/NPUZ,
     1                    MPI_DOUBLE_COMPLEX,ICOMMZ,IERR)
!$OMP END MASTER
!$OMP BARRIER
      END IF
!$OMP DO COLLAPSE(2) PRIVATE(I,K,KK)
      DO 120 II=1,NNXY*NNYZ,NBLK
        DO 110 KK=1,NZ,NBLK
          DO 100 I=II,MIN0(II+NBLK-1,NNXY*NNYZ)
!DIR$ VECTOR ALIGNED
            DO 90 K=KK,MIN0(KK+NBLK-1,NZ)
              BZXY(K,I)=A(I,K)
   90       CONTINUE
  100     CONTINUE
  110   CONTINUE
  120 CONTINUE
!$OMP DO PRIVATE(K,L)
      DO 150 I=1,NNXY*NNYZ
        CALL FFT235(BZXY(1,I),C,WZ,NZ,LNZ)
        DO 140 L=1,NPUZ
!DIR$ VECTOR ALIGNED
          DO 130 K=1,NNZZ
            AZXYP(K,I,L)=BZPXY(K,L,I)
  130     CONTINUE
  140   CONTINUE
  150 CONTINUE
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALL(AZXYP,NN/NPUZ,MPI_DOUBLE_COMPLEX,BZXY2,NN/NPUZ,
     1                  MPI_DOUBLE_COMPLEX,ICOMMZ,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO COLLAPSE(2) PRIVATE(J,JJ,K)
      DO 190 KK=1,NNZZ*NNXY,NBLK
        DO 180 JJ=1,NY,NBLK
          DO 170 K=KK,MIN0(KK+NBLK-1,NNZZ*NNXY)
!DIR$ VECTOR ALIGNED
            DO 160 J=JJ,MIN0(JJ+NBLK-1,NY)
              AYZX(J,K)=BZXY2(K,J)
  160       CONTINUE
  170     CONTINUE
  180   CONTINUE
  190 CONTINUE
!$OMP DO PRIVATE(J,L)
      DO 220 K=1,NNZZ*NNXY
        CALL FFT235(AYZX(1,K),C,WY,NY,LNY)
        DO 210 L=1,NPUY
!DIR$ VECTOR ALIGNED
          DO 200 J=1,NNYY
            BYZXP(J,K,L)=AYPZX(J,L,K)
  200     CONTINUE
  210   CONTINUE
  220 CONTINUE
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALL(BYZXP,NN/NPUY,MPI_DOUBLE_COMPLEX,AYZX2,NN/NPUY,
     1                  MPI_DOUBLE_COMPLEX,ICOMMY,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO COLLAPSE(2) PRIVATE(I,II,J)
      DO 260 JJ=1,NNYY*NNZZ,NBLK
        DO 250 II=1,NX,NBLK
          DO 240 J=JJ,MIN0(JJ+NBLK-1,NNYY*NNZZ)
!DIR$ VECTOR ALIGNED
            DO 230 I=II,MIN0(II+NBLK-1,NX)
              B(I,J)=AYZX2(J,I)
  230       CONTINUE
  240     CONTINUE
  250   CONTINUE
  260 CONTINUE
!$OMP DO
      DO 270 J=1,NNYY*NNZZ
        CALL FFT235(B(1,J),C,WX,NX,LNX)
  270 CONTINUE
      RETURN
      END
