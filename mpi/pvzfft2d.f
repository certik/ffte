C
C     FFTE: A FAST FOURIER TRANSFORM PACKAGE
C
C     (C) COPYRIGHT SOFTWARE, 2000-2004, 2008-2011, ALL RIGHTS RESERVED
C                BY
C         DAISUKE TAKAHASHI
C         FACULTY OF ENGINEERING, INFORMATION AND SYSTEMS
C         UNIVERSITY OF TSUKUBA
C         1-1-1 TENNODAI, TSUKUBA, IBARAKI 305-8573, JAPAN
C         E-MAIL: daisuke@cs.tsukuba.ac.jp
C
C
C     PARALLEL 2-D COMPLEX FFT ROUTINE (FOR VECTOR MACHINES)
C
C     FORTRAN77 + MPI SOURCE PROGRAM
C
C     CALL PZFFT2D(A,B,NX,NY,ICOMM,NPU,IOPT)
C
C     NX IS THE LENGTH OF THE TRANSFORMS IN THE X-DIRECTION (INTEGER*4)
C     NY IS THE LENGTH OF THE TRANSFORMS IN THE Y-DIRECTION (INTEGER*4)
C       ------------------------------------
C         NX = (2**IP) * (3**IQ) * (5**IR)
C         NY = (2**JP) * (3**JQ) * (5**JR)
C       ------------------------------------
C     ICOMM IS THE COMMUNICATOR (INTEGER*4)
C     NPU IS THE NUMBER OF PROCESSORS (INTEGER*4)
C     IOPT = 0 FOR INITIALIZING THE COEFFICIENTS (INTEGER*4)
C     IOPT = -1 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY/NPU) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,BLOCK)
C              B(NX,NY/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,BLOCK)
C     IOPT = +1 FOR INVERSE TRANSFORM WHERE
C              A(NX,NY/NPU) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,BLOCK)
C              B(NX,NY/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,BLOCK)
C     IOPT = -2 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY/NPU) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,BLOCK)
C              B(NX/NPU,NY) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(BLOCK,*)
C     IOPT = +2 FOR INVERSE TRANSFORM WHERE
C              A(NX/NPU,NY) IS COMPLEX INPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(BLOCK,*)
C              B(NX,NY/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,BLOCK)
C
C     WRITTEN BY DAISUKE TAKAHASHI
C
      SUBROUTINE PZFFT2D(A,B,NX,NY,ICOMM,NPU,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'param.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 WX(NDA2),WY(NDA2)
      DIMENSION LNX(3),LNY(3)
      SAVE WX,WY
C
      NN=NX*(NY/NPU)
C
      CALL FACTOR(NX,LNX)
      CALL FACTOR(NY,LNY)
C
      IF (IOPT .EQ. 0) THEN
        CALL SETTBL(WX,NX)
        CALL SETTBL(WY,NY)
        RETURN
      END IF
C
      IF (IOPT .EQ. 1 .OR. IOPT .EQ. 2) THEN
!DIR$ VECTOR ALIGNED
        DO 10 I=1,NN
          A(I)=DCONJG(A(I))
   10   CONTINUE
      END IF
C
      IF (IOPT .EQ. -1 .OR. IOPT .EQ. -2) THEN
        CALL PZFFT2DF(A,B,WX,WY,NX,NY,LNX,LNY,ICOMM,NPU,IOPT)
      ELSE
        CALL PZFFT2DB(A,B,WX,WY,NX,NY,LNX,LNY,ICOMM,NPU,IOPT)
      END IF
C
      IF (IOPT .EQ. 1 .OR. IOPT .EQ. 2) THEN
        DN=1.0D0/(DBLE(NX)*DBLE(NY))
!DIR$ VECTOR ALIGNED
        DO 20 I=1,NN
          B(I)=DCONJG(B(I))*DN
   20   CONTINUE
      END IF
      RETURN
      END
      SUBROUTINE PZFFT2DF(A,B,WX,WY,NX,NY,LNX,LNY,ICOMM,NPU,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 WX(*),WY(*)
      DIMENSION LNX(*),LNY(*)
C
      NNX=NX/NPU
      NNY=NY/NPU
      NN=NX*NNY
C
      CALL ZTRANS(A,B,NX,NNY)
      CALL MFFT235A(B,A,WX,NNY,NX,LNX)
      CALL MPI_ALLTOALL(B,NN/NPU,MPI_DOUBLE_COMPLEX,A,NN/NPU,
     1                  MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      CALL MZTRANSB(A,B,NNY,NNX,NPU)
      CALL MFFT235A(B,A,WY,NNX,NY,LNY)
      IF (IOPT .EQ. -2) RETURN
      CALL MPI_ALLTOALL(B,NN/NPU,MPI_DOUBLE_COMPLEX,A,NN/NPU,
     1                  MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      CALL MZTRANSA(A,B,NNX,NNY,NPU)
      RETURN
      END
      SUBROUTINE PZFFT2DB(A,B,WX,WY,NX,NY,LNX,LNY,ICOMM,NPU,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      COMPLEX*16 A(*),B(*)
      COMPLEX*16 WX(*),WY(*)
      DIMENSION LNX(*),LNY(*)
C
      NNX=NX/NPU
      NNY=NY/NPU
      NN=NX*NNY
C
      IF (IOPT .EQ. 1) THEN
        CALL MZTRANSA(A,B,NNX,NPU,NNY)
        CALL MPI_ALLTOALL(B,NN/NPU,MPI_DOUBLE_COMPLEX,A,NN/NPU,
     1                    MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      END IF
C
      CALL MFFT235A(A,B,WY,NNX,NY,LNY)
      CALL MZTRANSB(A,B,NNX,NNY,NPU)
      CALL MPI_ALLTOALL(B,NN/NPU,MPI_DOUBLE_COMPLEX,A,NN/NPU,
     1                  MPI_DOUBLE_COMPLEX,ICOMM,IERR)
      CALL MFFT235A(A,B,WX,NNY,NX,LNX)
      CALL ZTRANS(A,B,NNY,NX)
      RETURN
      END
