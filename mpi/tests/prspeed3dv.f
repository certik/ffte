C
C     FFTE: A FAST FOURIER TRANSFORM PACKAGE
C
C     (C) COPYRIGHT SOFTWARE, 2000-2004, 2008-2014, 2020
C         ALL RIGHTS RESERVED
C                BY
C         DAISUKE TAKAHASHI
C         CENTER FOR COMPUTATIONAL SCIENCES
C         UNIVERSITY OF TSUKUBA
C         1-1-1 TENNODAI, TSUKUBA, IBARAKI 305-8577, JAPAN
C         E-MAIL: daisuke@cs.tsukuba.ac.jp
C
C
C     PDZFFT3DV SPEED TEST PROGRAM
C
C     FORTRAN77 + MPI SOURCE PROGRAM
C
C     WRITTEN BY DAISUKE TAKAHASHI
C
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      PARAMETER (NDA=16777216,LOOP=10)
      DIMENSION A(NDA),B(NDA)
      DIMENSION LNX(3),LNY(3),LNZ(3),LNPU(3)
      SAVE A,B
C
      CALL MPI_INIT(IERR)
      CALL MPI_COMM_RANK(MPI_COMM_WORLD,ME,IERR)
      CALL MPI_COMM_SIZE(MPI_COMM_WORLD,NPU,IERR)
C
      CALL FACTOR(NPU,LNPU)
      NPUZ=(2**(LNPU(1)/2))*(3**(LNPU(2)/2))*(5**(LNPU(3)/2))
      NPUY=NPU/NPUZ
C
      CALL MPI_COMM_SPLIT(MPI_COMM_WORLD,ME/NPUY,0,ICOMMY,IERR)
      CALL MPI_COMM_SPLIT(MPI_COMM_WORLD,MOD(ME,NPUY),0,ICOMMZ,IERR)
      CALL MPI_COMM_RANK(ICOMMY,MEY,IERR)
C
      IF (ME .EQ. 0) THEN
        WRITE(6,*) ' NX,NY,NZ ='
        READ(5,*) NX,NY,NZ
      END IF
      CALL MPI_BCAST(NX,1,MPI_INTEGER,0,MPI_COMM_WORLD,IERR)
      CALL MPI_BCAST(NY,1,MPI_INTEGER,0,MPI_COMM_WORLD,IERR)
      CALL MPI_BCAST(NZ,1,MPI_INTEGER,0,MPI_COMM_WORLD,IERR)
      CALL FACTOR(NX,LNX)
      CALL FACTOR(NY,LNY)
      CALL FACTOR(NZ,LNZ)
C
      NN=NX*(NY/NPUY)*(NZ/NPUZ)
      CALL INIT(A,NN,ME,NPU)
      CALL PDZFFT3DV(A,B,NX,NY,NZ,ICOMMY,ICOMMZ,MEY,NPUY,NPUZ,0)
      CALL PDZFFT3DV(A,B,NX,NY,NZ,ICOMMY,ICOMMZ,MEY,NPUY,NPUZ,-1)
C
      CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
      IF (ME .EQ. 0) THEN
        TIME1=MPI_WTIME()
      END IF
      DO 10 I=1,LOOP
        CALL PDZFFT3DV(A,B,NX,NY,NZ,ICOMMY,ICOMMZ,MEY,NPUY,NPUZ,-1)
   10 CONTINUE
      CALL MPI_BARRIER(MPI_COMM_WORLD,IERR)
      IF (ME .EQ. 0) THEN
        TIME2=MPI_WTIME()
        TIME0=(TIME2-TIME1)/DBLE(LOOP)
      END IF
      IF (ME .EQ. 0) THEN
        FLOPS=(2.5D0*DBLE(LNX(1)+LNY(1)+LNZ(1))
     1         +4.66666666666666D0*DBLE(LNX(2)+LNY(2)+LNZ(2))
     2         +6.8D0*DBLE(LNX(3)+LNY(3)+LNZ(3)))
     3         *DBLE(NX)*DBLE(NY)*DBLE(NZ)/TIME0/1.0D9
        WRITE(6,*) ' NPU =',NPU
        WRITE(6,*) ' NX =',NX,' NY =',NY,' NZ =',NZ
        WRITE(6,*) ' TIME =',TIME0
        WRITE(6,*) FLOPS,' GFLOPS'
      END IF
C
      CALL MPI_COMM_FREE(ICOMMY,IERR)
      CALL MPI_COMM_FREE(ICOMMZ,IERR)
      CALL MPI_FINALIZE(IERR)
      STOP
      END
      SUBROUTINE INIT(A,NN,ME,NPU)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(*)
      INTEGER*8 N
C
      N=NN
      N=N*NPU
!$OMP PARALLEL DO
!DIR$ VECTOR ALIGNED
      DO 10 I=1,NN
C        A(I)=DBLE(I)+DBLE(NN)*DBLE(ME)
        A(I)=0.0D0
   10 CONTINUE
      RETURN
      END
