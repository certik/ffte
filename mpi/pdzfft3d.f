C
C     FFTE: A FAST FOURIER TRANSFORM PACKAGE
C
C     (C) COPYRIGHT SOFTWARE, 2000-2004, 2008-2011, 2020
C         ALL RIGHTS RESERVED
C                BY
C         DAISUKE TAKAHASHI
C         CENTER FOR COMPUTATIONAL SCIENCES
C         UNIVERSITY OF TSUKUBA
C         1-1-1 TENNODAI, TSUKUBA, IBARAKI 305-8577, JAPAN
C         E-MAIL: daisuke@cs.tsukuba.ac.jp
C
C
C     PARALLEL 3-D REAL-TO-COMPLEX FFT ROUTINE
C
C     FORTRAN77 + MPI SOURCE PROGRAM
C
C     CALL PDZFFT3D(A,B,NX,NY,NZ,ICOMM,ME,NPU,IOPT)
C
C     NX IS THE LENGTH OF THE TRANSFORMS IN THE X-DIRECTION (INTEGER*4)
C     NY IS THE LENGTH OF THE TRANSFORMS IN THE Y-DIRECTION (INTEGER*4)
C     NZ IS THE LENGTH OF THE TRANSFORMS IN THE Z-DIRECTION (INTEGER*4)
C       ------------------------------------
C         NX = (2**IP) * (3**IQ) * (5**IR)
C         NY = (2**JP) * (3**JQ) * (5**JR)
C         NZ = (2**KP) * (3**KQ) * (5**KR)
C       ------------------------------------
C     ICOMM IS THE COMMUNICATOR (INTEGER*4)
C     ME IS THE RANK (INTEGER*4)
C     NPU IS THE NUMBER OF PROCESSORS (INTEGER*4)
C     IOPT = 0 FOR INITIALIZING THE COEFFICIENTS (INTEGER*4)
C     IOPT = -1 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY,NZ/NPU) IS REAL INPUT VECTOR (REAL*8)
C!HPF$ DISTRIBUTE A(*,*,BLOCK)
C              A(NX/2+1,NY,NZ/NPU) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(*,*,BLOCK)
C              B(NX/2+1,NY,NZ/NPU) IS WORK VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,*,BLOCK)
C     IOPT = -2 FOR FORWARD TRANSFORM WHERE
C              A(NX,NY,NZ/NPU) IS REAL INPUT VECTOR (REAL*8)
C!HPF$ DISTRIBUTE A(*,*,BLOCK)
C     ME = 0   A((NX/2)/NPU+1,NY,NZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C     ME > 0   A((NX/2)/NPU,NY,NZ) IS COMPLEX OUTPUT VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE A(BLOCK,*,*)
C              B(NX/2+1,NY,NZ/NPU) IS WORK VECTOR (COMPLEX*16)
C!HPF$ DISTRIBUTE B(*,*,BLOCK)
C
C     WRITTEN BY DAISUKE TAKAHASHI
C
      SUBROUTINE PDZFFT3D(A,B,NX,NY,NZ,ICOMM,ME,NPU,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'param.h'
      DIMENSION A(*)
      COMPLEX*16 B(*)
      COMPLEX*16 C((NDA3+NP)*NBLK),D(NDA3)
      COMPLEX*16 WX(NDA3),WY(NDA3),WZ(NDA3)
      DIMENSION LNX(3),LNY(3),LNZ(3)
      SAVE WX,WY,WZ
C
      IF (IOPT .EQ. 0) THEN
        CALL SETTBL(WX,NX)
        CALL SETTBL(WY,NY)
        CALL SETTBL(WZ,NZ)
        RETURN
      END IF
C
      CALL FACTOR(NX,LNX)
      CALL FACTOR(NY,LNY)
      CALL FACTOR(NZ,LNZ)
C
!$OMP PARALLEL PRIVATE(C,D)
      CALL PDZFFT3D0(A,A,A,B,B,C,C,C,D,WX,WY,WZ,NX,NY,NZ,LNX,LNY,LNZ,
     1               ICOMM,ME,NPU,IOPT)
!$OMP END PARALLEL
      RETURN
      END
      SUBROUTINE PDZFFT3D0(DA,A,AXYZ,B,BXYZ,CX,CY,CZ,D,WX,WY,WZ,
     1                     NX,NY,NZ,LNX,LNY,LNZ,ICOMM,ME,NPU,IOPT)
      IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'mpif.h'
      INCLUDE 'param.h'
      COMPLEX*16 A(*),AXYZ(NX/2+1,NY,*)
      COMPLEX*16 B(*),BXYZ(NX/2+1,NY,*)
      COMPLEX*16 CX(*),CY(NY+NP,*),CZ(NZ+NP,*),D(*)
      COMPLEX*16 WX(*),WY(*),WZ(*)
      DIMENSION DA(NX,NY,*)
      DIMENSION ISCNT(MAXNPU),ISDSP(MAXNPU),IRCNT(MAXNPU),IRDSP(MAXNPU)
      DIMENSION LNX(*),LNY(*),LNZ(*)
C
      NNX=NX/NPU
      NNZ=NZ/NPU
C
      ISCNT(1)=(NNX/2+1)*NY*NNZ
      ISDSP(1)=0
      DO 10 I=2,NPU
        ISCNT(I)=(NNX/2)*NY*NNZ
        ISDSP(I)=ISDSP(I-1)+ISCNT(I-1)
   10 CONTINUE
      IF (ME .EQ. 0) THEN
        IRCNT(1)=(NNX/2+1)*NY*NNZ
        IRDSP(1)=0
        DO 20 I=2,NPU
          IRCNT(I)=(NNX/2+1)*NY*NNZ
          IRDSP(I)=IRDSP(I-1)+IRCNT(I-1)
   20   CONTINUE
      ELSE
        IRCNT(1)=(NNX/2)*NY*NNZ
        IRDSP(1)=0
        DO 30 I=2,NPU
          IRCNT(I)=(NNX/2)*NY*NNZ
          IRDSP(I)=IRDSP(I-1)+IRCNT(I-1)
   30   CONTINUE
      END IF
C
      IF (MOD(NY,2) .EQ. 0) THEN
!$OMP DO COLLAPSE(2) PRIVATE(I,J)
        DO 70 K=1,NNZ
          DO 60 J=1,NY,2
            DO 40 I=1,NX
              CX(I)=DCMPLX(DA(I,J,K),DA(I,J+1,K))
   40       CONTINUE
            CALL FFT235(CX,D,WX,NX,LNX)
            BXYZ(1,J,K)=DBLE(CX(1))
            BXYZ(1,J+1,K)=DIMAG(CX(1))
!DIR$ VECTOR ALIGNED
            DO 50 I=2,NX/2+1
              BXYZ(I,J,K)=0.5D0*(CX(I)+DCONJG(CX(NX-I+2)))
              BXYZ(I,J+1,K)=(0.0D0,-0.5D0)*(CX(I)-DCONJG(CX(NX-I+2)))
   50       CONTINUE
   60     CONTINUE
   70   CONTINUE
      ELSE
!$OMP DO COLLAPSE(2) PRIVATE(I,J)
        DO 110 K=1,NNZ
          DO 100 J=1,NY-1,2
            DO 80 I=1,NX
              CX(I)=DCMPLX(DA(I,J,K),DA(I,J+1,K))
   80       CONTINUE
            CALL FFT235(CX,D,WX,NX,LNX)
            BXYZ(1,J,K)=DBLE(CX(1))
            BXYZ(1,J+1,K)=DIMAG(CX(1))
!DIR$ VECTOR ALIGNED
            DO 90 I=2,NX/2+1
              BXYZ(I,J,K)=0.5D0*(CX(I)+DCONJG(CX(NX-I+2)))
              BXYZ(I,J+1,K)=(0.0D0,-0.5D0)*(CX(I)-DCONJG(CX(NX-I+2)))
   90       CONTINUE
  100     CONTINUE
  110   CONTINUE
!$OMP DO PRIVATE(I)
        DO 140 K=1,NNZ
          DO 120 I=1,NX
            CX(I)=DCMPLX(DA(I,NY,K),0.0D0)
  120     CONTINUE
          CALL FFT235(CX,D,WX,NX,LNX)
!DIR$ VECTOR ALIGNED
          DO 130 I=1,NX/2+1
            BXYZ(I,NY,K)=CX(I)
  130     CONTINUE
  140   CONTINUE
      END IF
!$OMP DO COLLAPSE(2) PRIVATE(I,II,J,JJ)
      DO 220 K=1,NNZ
        DO 210 II=1,NNX/2+1,NBLK
          DO 170 JJ=1,NY,NBLK
            DO 160 I=II,MIN0(II+NBLK-1,NNX/2+1)
!DIR$ VECTOR ALIGNED
              DO 150 J=JJ,MIN0(JJ+NBLK-1,NY)
                CY(J,I-II+1)=BXYZ(I,J,K)
  150         CONTINUE
  160       CONTINUE
  170     CONTINUE
          DO 180 I=II,MIN0(II+NBLK-1,NNX/2+1)
            CALL FFT235(CY(1,I-II+1),D,WY,NY,LNY)
  180     CONTINUE
          DO 200 J=1,NY
!DIR$ VECTOR ALIGNED
            DO 190 I=II,MIN0(II+NBLK-1,NNX/2+1)
              A(I+(J-1)*(NNX/2+1)+(K-1)*(NNX/2+1)*NY)=CY(J,I-II+1)
  190       CONTINUE
  200     CONTINUE
  210   CONTINUE
  220 CONTINUE
!$OMP DO COLLAPSE(2) PRIVATE(I,II,J,JJ,L)
      DO 310 K=1,NNZ
        DO 300 L=2,NPU
          DO 290 II=1,NNX/2,NBLK
            DO 250 JJ=1,NY,NBLK
              DO 240 I=II,MIN0(II+NBLK-1,NNX/2)
!DIR$ VECTOR ALIGNED
                DO 230 J=JJ,MIN0(JJ+NBLK-1,NY)
                  CY(J,I-II+1)=BXYZ(I+(L-1)*(NNX/2)+1,J,K)
  230           CONTINUE
  240         CONTINUE
  250       CONTINUE
            DO 260 I=II,MIN0(II+NBLK-1,NNX/2)
              CALL FFT235(CY(1,I-II+1),D,WY,NY,LNY)
  260       CONTINUE
            DO 280 J=1,NY
!DIR$ VECTOR ALIGNED
              DO 270 I=II,MIN0(II+NBLK-1,NNX/2)
                A(I+(J-1)*(NNX/2)+(K-1)*(NNX/2)*NY
     1            +((L-1)*(NNX/2)+1)*NY*NNZ)=CY(J,I-II+1)
  270         CONTINUE
  280       CONTINUE
  290     CONTINUE
  300   CONTINUE
  310 CONTINUE
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALLV(A,ISCNT,ISDSP,MPI_DOUBLE_COMPLEX,B,IRCNT,IRDSP,
     1                   MPI_DOUBLE_COMPLEX,ICOMM,IERR)
!$OMP END MASTER
!$OMP BARRIER
      IF (ME .EQ. 0) THEN
!$OMP DO PRIVATE(I,II,K,L)
        DO 390 J=1,NY
          DO 380 II=1,NNX/2+1,NBLK
            DO 340 L=1,NPU
              DO 330 I=II,MIN0(II+NBLK-1,NNX/2+1)
!DIR$ VECTOR ALIGNED
                DO 320 K=1,NNZ
                  CZ(K+(L-1)*NNZ,I-II+1)
     1           =B(I+(J-1)*(NNX/2+1)+(K-1)*(NNX/2+1)*NY
     2              +(L-1)*(NNX/2+1)*NY*NNZ)
  320           CONTINUE
  330         CONTINUE
  340       CONTINUE
            DO 350 I=II,MIN0(II+NBLK-1,NNX/2+1)
              CALL FFT235(CZ(1,I-II+1),D,WZ,NZ,LNZ)
  350       CONTINUE
            DO 370 K=1,NZ
!DIR$ VECTOR ALIGNED
              DO 360 I=II,MIN0(II+NBLK-1,NNX/2+1)
                A(I+(J-1)*(NNX/2+1)+(K-1)*(NNX/2+1)*NY)=CZ(K,I-II+1)
  360         CONTINUE
  370       CONTINUE
  380     CONTINUE
  390   CONTINUE
      ELSE
!$OMP DO PRIVATE(I,II,K,L)
        DO 470 J=1,NY
          DO 460 II=1,NNX/2,NBLK
            DO 420 L=1,NPU
              DO 410 I=II,MIN0(II+NBLK-1,NNX/2)
!DIR$ VECTOR ALIGNED
                DO 400 K=1,NNZ
                  CZ(K+(L-1)*NNZ,I-II+1)
     1           =B(I+(J-1)*(NNX/2)+(K-1)*(NNX/2)*NY
     2              +(L-1)*(NNX/2)*NY*NNZ)
  400           CONTINUE
  410         CONTINUE
  420       CONTINUE
            DO 430 I=II,MIN0(II+NBLK-1,NNX/2)
              CALL FFT235(CZ(1,I-II+1),D,WZ,NZ,LNZ)
  430       CONTINUE
            DO 450 K=1,NZ
!DIR$ VECTOR ALIGNED
              DO 440 I=II,MIN0(II+NBLK-1,NNX/2)
                A(I+(J-1)*(NNX/2)+(K-1)*(NNX/2)*NY)=CZ(K,I-II+1)
  440         CONTINUE
  450       CONTINUE
  460     CONTINUE
  470   CONTINUE
      END IF
      IF (IOPT .EQ. -2) RETURN
!$OMP BARRIER
!$OMP MASTER
      CALL MPI_ALLTOALLV(A,IRCNT,IRDSP,MPI_DOUBLE_COMPLEX,B,ISCNT,ISDSP,
     1                   MPI_DOUBLE_COMPLEX,ICOMM,IERR)
!$OMP END MASTER
!$OMP BARRIER
!$OMP DO COLLAPSE(2) PRIVATE(I,J,L)
      DO 520 K=1,NNZ
        DO 510 J=1,NY
!DIR$ VECTOR ALIGNED
          DO 480 I=1,NNX/2+1
            AXYZ(I,J,K)=B(I+(J-1)*(NNX/2+1)+(K-1)*(NNX/2+1)*NY)
  480     CONTINUE
          DO 500 L=2,NPU
!DIR$ VECTOR ALIGNED
            DO 490 I=1,NNX/2
              AXYZ(I+(L-1)*(NNX/2)+1,J,K)
     1       =B(I+(J-1)*(NNX/2)+(K-1)*(NNX/2)*NY
     2          +((L-1)*(NNX/2)+1)*NY*NNZ)
  490       CONTINUE
  500     CONTINUE
  510   CONTINUE
  520 CONTINUE
      RETURN
      END
